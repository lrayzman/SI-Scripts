// ======================   S-params Converter ====================
// 
// (Semi)Intelligent Differential S-param Viewer
// 
//  Main GUI controls
//
// (c)2014  L. Rayzman
//
//  GUI interface based on UICONTROL2 GUI demo
//
// Created      : 02/25/2014
// Last Update  : 06/23/2014   - Separated GUI controls into separate code file
//                             - Updated for compatibility with 5.5.0
//
// ====================================================================
// ====================================================================


///////////////////////////////////////////////////////////////////////////////
////////////////////////////VIEWER GUI CREATION   ////////////////////////////
///////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
//
// Main Figure creation
//
//

function main_gui_event_handler(win, x, y, ibut)
     if ibut==-1000 then quit, end    
endfunction

function on_help_click()
   messagebox(["SxP InteViewer" "(c)2014   L.Rayzman" strcat(["v" string(SxPversion)])], "About...", "info");
endfunction



gui_axes_w          = 2*gui_margin_x + gui_frame_w;                    // Axes width
gui_axes_h          = 2*gui_margin_y + gui_frame_h;                    // Axes height



// Clean up existing figure if it exists
try
    delete(main_GUI_fig);
end    


main_GUI_fig = figure("figure_id", main_GUI_fig_idx, ...
                        "dockable", "off" ,... 
                        "infobar_visible", "off", ...
                        "toolbar_visible", "off", ...
                        "menubar_visible", "on", ...
                        "position", [100 100 gui_axes_w gui_axes_h], ...
                        "resize", "off",...
                        "event_handler", "main_gui_event_handler",...
                        "event_handler_enable", "on");


drawlater();

// Remove default Scilab graphics menus and toolbar

delmenu(main_GUI_fig.figure_id, gettext("&File"));
delmenu(main_GUI_fig.figure_id, gettext("&Tools"));
delmenu(main_GUI_fig.figure_id, gettext("&Edit"));
delmenu(main_GUI_fig.figure_id, gettext("&?"));
//toolbar(main_GUI_fig.figure_id, "off");

//main_GUI_fig.background =  -2;
//main_GUI_fig.color_map = jetcolormap(128);
//main_GUI_fig.figure_position = [100 100];
main_GUI_fig.figure_name = gettext(strcat(["SxP InteViewer v" string(SxPversion)]));

// Create File menu

gui_menu = uimenu(main_GUI_fig,...                // File Menu
                  "label", gettext("File")); 

uimenu (gui_menu, ...                             // File->Load New File.. Menu
        "label",  "Load New File...", ...
        "tag", "load_new_file_menu", ...
        "callback", strcat(["delete(main_GUI_fig);exec(""" SxPExecName """)"]));



uimenu (gui_menu, ...                             // File->Close Menu
        "label",  "Close", ...
        "tag", "close_menu", ...
        "callback", "delete(main_GUI_fig);if getscilabmode()==""NW"" then quit() end");
        
gui_menu= uimenu (main_GUI_fig, ...               // ? Menu
                  "label",  gettext("?"));
                  
uimenu (gui_menu, ...                             // ?->About
        "label",  "About", ...
        "tag", "about_menu", ...
        "callback", "on_help_click()");        


///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
//
// Plot Control Frame
//
//

main_GUI_handles.viewer_frame = uicontrol( ...
                          "parent", main_GUI_fig, ...
                          "relief", "groove", ...
                          "style", "frame", ...
                          "units", "pixels", ...
                          "position", [gui_margin_x gui_margin_y gui_frame_w gui_frame_h], ...
                          "horizontalalignment", "center", ...
                          "tag", "frame_control");
                          
                          
//main_GUI_handles.viewer_frame_title = uicontrol (...
//                                "parent", main_GUI_fig, ...
//                                "style", "text", ...
//                                "string", "Plot Controls", ...
//                                "units", "pixels", ...
//                                "position", [ 30+gui_margin_x gui_margin_y+gui_frame_h-10 gui_frame_w-60 20], ...
//                                "fontname", gui_defaultfont, ...
//                                "fontunits", "points", ...
//                                "fontsize", 14, ...
//                                "horizontalalignment", "center", ...
//                                "tag", "title_plot_control");
                                
                                
                                

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
//
// Victim Port Frame
//
//

main_GUI_handles.viewer_frame_victim = uicontrol( ...
                          "parent", main_GUI_fig, ...
                          "relief", "groove", ...
                          "style", "frame", ...
                          "units", "pixels", ...
                          "position", [30+gui_margin_x gui_margin_y+gui_frame_h-100 gui_frame_w-60 80], ...
                          "horizontalalignment", "center", ...
                          "tag", "frame_victim");
                          
                          
main_GUI_handles.viewer_frame_vict_title = uicontrol (...
                                "parent", main_GUI_fig, ...
                                "style", "text", ...
                                "string", "Victim Ports", ...
                                "units", "pixels", ...
                                "position", [40+gui_margin_x gui_margin_y+gui_frame_h-30 gui_frame_w-80 20], ...
                                "fontname", gui_defaultfont, ...
                                "fontunits", "points", ...
                                "fontsize", 12, ...
                                "horizontalalignment", "center", ...
                                "tag", "title_victim_ports");
                               
//                                
// Generate ports list text for the list
//
portslist=emptystr();

if smapmode==1 then    // Odd/Even mapping
    for i=1:(numofports/4)
        portslist(i)=strcat([string(i) "[" string((i-1)*4+1) "," string((i-1)*4+3) "]"]);
    end
    for i=1:(numofports/4)
        portslist(i+numofports/4)=strcat([string(i+numofports/4) "[" string((i-1)*4+2) "," string((i-1)*4+4) "]"]);
    end    
    
elseif smapmode==2 then         // Sequential mapping
    for i=1:(numofports/2)
        portslist(i)=strcat([string(i) "[" string((i-1)*2+1) "," string((i-1)*2+2) "]"]);
    end
    
   
end


// Input and Output Ports selection handlers
function on_inportslist_click(handles)
    
    
    global numofports;
    // Get current value
    idx=handles.viewer_list_inports.Value;
    
        
    // Figure out idx of the output port
    
    if idx <= numofports/4 then
        idx = idx + numofports/4;
    else
        idx = idx - numofports/4;
    end
    
    // Update inports list    
    handles.viewer_list_outports.Value = idx;    
    handles.viewer_list_outports.ListboxTop = idx;
endfunction


function on_outportslist_click(handles)
    
    global numofports;
    
    // Get current value
    idx=handles.viewer_list_outports.Value;
        
    // Figure out idx of the input port
    
    if idx <= numofports/4 then
        idx = idx + numofports/4;
    else
        idx = idx - numofports/4;
    end
    
    // Update inports list
    handles.viewer_list_inports.ListboxTop = idx;
    handles.viewer_list_inports.Value = idx;

endfunction
          


main_GUI_handles.viewer_text_inports =  uicontrol (...
                                "parent", main_GUI_fig, ...
                                "style", "text", ...
                                "units", "pixels", ...
                                "string", "Input Ports", ...                                
                                "position", [65+gui_margin_x gui_margin_y+gui_frame_h-48 50 19], ...
                                "fontname", gui_defaultfont, ...
                                "fontunits", "points", ...
                                "fontsize", 11, ...
                                "horizontalalignment", "center", ...
                                "tag", "text_inports");   
                                          
                                
main_GUI_handles.viewer_list_inports = uicontrol("parent", main_GUI_fig, ...
                                                 "style", "popupmenu", ...
                                                 "string", portslist, ...
                                                 "units", "pixels",...
                                                 "position", [45+gui_margin_x gui_margin_y+gui_frame_h-70 90 20], ...
                                                 "fontunits", "points", ...
                                                 "fontsize", 12, ...
                                                 "background", [0.9 0.9 0.9], ...
                                                 "value", 1, ..
                                                 "tag", "listbox_inports", ...
                                                 "callback", "on_inportslist_click(main_GUI_handles)");    
                                                 
                                                 
main_GUI_handles.viewer_text_outports =  uicontrol (...
                                "parent", main_GUI_fig, ...
                                "style", "text", ...
                                "units", "pixels", ...
                                "string", "Output Ports", ...                                
                                "position", [170+gui_margin_x gui_margin_y+gui_frame_h-48 60 19], ...
                                "fontname", gui_defaultfont, ...
                                "fontunits", "points", ...
                                "fontsize", 11, ...
                                "horizontalalignment", "center", ...
                                "tag", "text_outports");   
                                                                                                                                                                               
                                
main_GUI_handles.viewer_list_outports =  uicontrol ("parent", main_GUI_fig, ...
                                                     "style", "popupmenu", ...
                                                     "units", "pixels", ...
                                                     "string", portslist, ...                                                     
                                                     "position", [160+gui_margin_x gui_margin_y+gui_frame_h-70 90 20], ...
                                                     "fontunits", "points", ...
                                                     "fontsize", 12, ...
                                                     "background", [0.9 0.9 0.9], ...
                                                     "value", numofports/4+1, ..
                                                     "tag", "listbox_outports", ...
                                                     "callback", "on_outportslist_click(main_GUI_handles)");
                                                     
main_GUI_handles.viewer_text_portsarrow =  uicontrol ("parent", main_GUI_fig, ...
                                                       "style", "text", ...
                                                       "units", "pixels", ...
                                                       "string", "$\Rightarrow$", ...                                
                                                       "position", [135+gui_margin_x gui_margin_y+gui_frame_h-70 20 19], ...
                                                       "fontname", gui_defaultfont, ...
                                                       "fontunits", "points", ...
                                                       "fontsize", 15, ...
                                                       "horizontalalignment", "center", ...
                                                       "tag", "text_portsarrow");                                                        
                                

                                
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
//
// Plot Mode Frame
//
//



main_GUI_handles.viewer_frame_plotmode = uicontrol( ...
                          "parent", main_GUI_fig, ...
                          "relief", "groove", ...
                          "style", "frame", ...
                          "units", "pixels", ...
                          "position", [30+gui_margin_x gui_margin_y+gui_frame_h-220 gui_frame_w-60 100], ...
                          "horizontalalignment", "center", ...
                          "tag", "frame_");
                          
                          
main_GUI_handles.viewer_frame_pmde_title = uicontrol (...
                                "parent", main_GUI_fig, ...
                                "style", "text", ...
                                "string", "Plot Mode", ...
                                "units", "pixels", ...
                                "position", [40+gui_margin_x gui_margin_y+gui_frame_h-135 gui_frame_w-80 20], ...
                                "fontname", gui_defaultfont, ...
                                "fontunits", "points", ...
                                "fontsize", 12, ...
                                "horizontalalignment", "center", ...
                                "tag", "title_plot_mode");
                                
main_GUI_handles.viewer_frame_RadioIL = uicontrol (...
                                "parent", main_GUI_fig, ...
                                "style", "radiobutton", ...
                                "groupname", "radio_plotmode", ...
                                "string", "IL", ...
                                "units", "pixels", ...
                                "position", [60+gui_margin_x gui_margin_y+gui_frame_h-155 70 20], ...
                                "fontname", gui_defaultfont, ...
                                "fontunits", "points", ...
                                "fontsize", 12, ...
                                "VerticalAlignment", "middle", ...
                                "HorizontalAlignment","left", ...                                
                                "tag", "radio_IL");     

main_GUI_handles.viewer_frame_RadioRLIN = uicontrol (...
                                "parent", main_GUI_fig, ...
                                "style", "radiobutton", ...
                                "groupname", "radio_plotmode", ...                                
                                "string", "RL IN", ...
                                "units", "pixels", ...
                                "position", [60+gui_margin_x gui_margin_y+gui_frame_h-155-gui_margin_y-gui_padding_y 70 20], ...
                                "fontname", gui_defaultfont, ...
                                "fontunits", "points", ...
                                "fontsize", 12, ...
                                "VerticalAlignment", "middle", ...
                                "HorizontalAlignment","left", ...                                
                                "tag", "radio_RLIN");
  
                                
main_GUI_handles.viewer_frame_RadioRLOUT = uicontrol (...
                                "parent", main_GUI_fig, ...
                                "style", "radiobutton", ...
                                "groupname", "radio_plotmode", ...                                
                                "string", "RL OUT", ...
                                "units", "pixels", ...
                                "position", [60+gui_margin_x gui_margin_y+gui_frame_h-155-2*(gui_margin_y+gui_padding_y) 70 20], ...
                                "fontname", gui_defaultfont, ...
                                "fontunits", "points", ...
                                "fontsize", 12, ...
                                "VerticalAlignment", "middle", ...
                                "HorizontalAlignment","left", ...
                                "tag", "radio_RLOUT");
                                
main_GUI_handles.viewer_frame_RadioNEXT = uicontrol (...
                                "parent", main_GUI_fig, ...
                                "style", "radiobutton", ...
                                "groupname", "radio_plotmode", ...                                
                                "string", "NEXT", ...
                                "units", "pixels", ...
                                "position", [160+gui_margin_x  gui_margin_y+gui_frame_h-155 70 20], ...
                                "fontname", gui_defaultfont, ...
                                "fontunits", "points", ...
                                "fontsize", 12, ...
                                "VerticalAlignment", "middle", ...
                                "HorizontalAlignment","left", ...                                
                                "tag", "radio_NEXT");

main_GUI_handles.viewer_frame_RadioFEXT = uicontrol (...
                                "parent", main_GUI_fig, ...
                                "style", "radiobutton", ...
                                "groupname", "radio_plotmode", ...                                
                                "string", "FEXT", ...
                                "units", "pixels", ...
                                "position", [160+gui_margin_x  gui_margin_y+gui_frame_h-155-gui_margin_y-gui_padding_y 70 20], ...
                                "fontname", gui_defaultfont, ...
                                "fontunits", "points", ...
                                "fontsize", 12, ...
                                "VerticalAlignment", "middle", ...
                                "HorizontalAlignment","left", ...                                
                                "tag", "radio_FEXT");
                                
main_GUI_handles.viewer_frame_checkGD = uicontrol (...
                                "parent", main_GUI_fig, ...
                                "style", "checkbox", ...
                                "string", "Group Delay", ...
                                "units", "pixels", ...
                                "position", [160+gui_margin_x  gui_margin_y+gui_frame_h-155-2*(gui_margin_y+gui_padding_y) 100 20], ...
                                "fontname", gui_defaultfont, ...
                                "fontunits", "points", ...
                                "fontsize", 12, ...
                                "VerticalAlignment", "middle", ...
                                "HorizontalAlignment","left", ...                                
                                "ForegroundColor", [0.6 0.6 0.6], ...
                                "Value", 0, ... 
                                "Enable", "off", ...                               
                                "Visible", "off", ...                                  
                                "tag", "check_GD");     

main_GUI_handles.viewer_frame_RadioIL.value=1;                                                             
                                

// Disable NEXT and FEXT calculation for 4-port 
if numofports==4 then
    main_GUI_handles.viewer_frame_RadioNEXT.Enable=0;
    main_GUI_handles.viewer_frame_RadioNEXT.ForegroundColor=[0.6 0.6 0.6];
    main_GUI_handles.viewer_frame_RadioFEXT.Enable=0;    
    main_GUI_handles.viewer_frame_RadioFEXT.ForegroundColor=[0.6 0.6 0.6]; 
end


///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
//
// SxP MixMode Frame
//
//



main_GUI_handles.viewer_frame_mxmode = uicontrol( ...
                          "parent", main_GUI_fig, ...
                          "relief", "groove", ...
                          "style", "frame", ...
                          "units", "pixels", ...
                          "position", [30+gui_margin_x gui_margin_y+gui_frame_h-310 gui_frame_w-60 70], ...
                          "horizontalalignment", "center", ...
                          "tag", "frame_mixemode");
                          
                          
main_GUI_handles.viewer_frame_mxmode_ttl = uicontrol (...
                                "parent", main_GUI_fig, ...
                                "style", "text", ...
                                "string", "Mixed Mode", ...
                                "units", "pixels", ...
                                "position", [40+gui_margin_x gui_margin_y+gui_frame_h-250 gui_frame_w-80 20], ...
                                "fontname", gui_defaultfont, ...
                                "fontunits", "points", ...
                                "fontsize", 12, ...
                                "horizontalalignment", "center", ...
                                "tag", "title_mixmode");


main_GUI_handles.viewer_frame_RadioSDD = uicontrol (...
                                "parent", main_GUI_fig, ...
                                "style", "radiobutton", ...
                                "groupname", "radio_mixmode", ...
                                "string", "SDD", ...
                                "units", "pixels", ...
                                "position", [60+gui_margin_x gui_margin_y+gui_frame_h-275 70 20], ...
                                "fontname", gui_defaultfont, ...
                                "fontunits", "points", ...
                                "fontsize", 12, ...
                                "VerticalAlignment", "middle", ...
                                "HorizontalAlignment","left", ...                                
                                "tag", "radio_SDD");
//                                "Callback", "on_radioSDD_click(main_GUI_handles)"); 

                                
main_GUI_handles.viewer_frame_RadioSCD = uicontrol (...
                                "parent", main_GUI_fig, ...
                                "style", "radiobutton", ...
                                "groupname", "radio_mixmode", ...                                
                                "string", "SCD", ...
                                "units", "pixels", ...
                                "position", [60+gui_margin_x gui_margin_y+gui_frame_h-275-gui_margin_y-gui_padding_y 70 20], ...
                                "fontname", gui_defaultfont, ...
                                "fontunits", "points", ...
                                "fontsize", 12, ...
                                "VerticalAlignment", "middle", ...
                                "HorizontalAlignment","left", ...                                
                                "tag", "radio_SCD");
//                                "Callback", "on_radioSCD_click(main_GUI_handles)");                   
                                
main_GUI_handles.viewer_frame_RadioSDC = uicontrol (...
                                "parent", main_GUI_fig, ...
                                "style", "radiobutton", ...
                                "groupname", "radio_mixmode", ...                                
                                "string", "SDC", ...
                                "units", "pixels", ...
                                "position", [160+gui_margin_x gui_margin_y+gui_frame_h-275 70 20], ...
                                "fontname", gui_defaultfont, ...
                                "fontunits", "points", ...
                                "fontsize", 12, ...
                                "VerticalAlignment", "middle", ...
                                "HorizontalAlignment","left", ...                                
                                "tag", "radio_SDC");
//                                "Callback", "on_radioSDC_click(main_GUI_handles)");     
                                
main_GUI_handles.viewer_frame_RadioSCC = uicontrol (...
                                "parent", main_GUI_fig, ...
                                "style", "radiobutton", ...
                                "groupname", "radio_mixmode", ...                                
                                "string", "SCC", ...
                                "units", "pixels", ...
                                "position", [160+gui_margin_x gui_margin_y+gui_frame_h-275-gui_margin_y-gui_padding_y 70 20], ...
                                "fontname", gui_defaultfont, ...
                                "fontunits", "points", ...
                                "fontsize", 12, ...
                                "VerticalAlignment", "middle", ...
                                "HorizontalAlignment","left", ...                                
                                "tag", "radio_SCC");
//                                "Callback", "on_radioSCC_click(main_GUI_handles)");   

main_GUI_handles.viewer_frame_RadioSDD.value=1;
                                
                                
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
//
// Port Impedance Frame
//
//                                                                                                           


pz_edit_curZ=50;                                  // Current impedance value

function on_pzedit_click(handles)
    
    if  ~(isnum(handles.viewer_frame_pz_edit.string)) then 
        handles.viewer_frame_pz_edit.string=string(pz_edit_curZ);
        sleep(50);
        messagebox("Please enter a positive real number");        
    elseif(evstr(handles.viewer_frame_pz_edit.string) <= 0) then
        messagebox("Please enter a positive real number");
        handles.viewer_frame_pz_edit.string=string(pz_edit_curZ);
    else
        pz_edit_curZ=evstr(handles.viewer_frame_pz_edit.string);  
    end
   
endfunction

main_GUI_handles.viewer_frame_pz = uicontrol("parent", main_GUI_fig, ...
                                                "relief", "groove", ...
                                                "style", "frame", ...
                                                "units", "pixels", ...
                                                "position", [30+gui_margin_x gui_margin_y+gui_frame_h-370 gui_frame_w-60 40], ...
                                                "horizontalalignment", "center", ...
                                                "tag", "frame_portz");


main_GUI_handles.viewer_frame_pz_ttl = uicontrol ("parent", main_GUI_fig, ...
                                                     "style", "text", ...
                                                     "string", "Port Impedance", ...
                                                     "units", "pixels", ...
                                                     "position", [40+gui_margin_x gui_margin_y+gui_frame_h-340 gui_frame_w-80 20], ...
                                                     "fontname", gui_defaultfont, ...
                                                     "fontunits", "points", ...
                                                     "fontsize", 12, ...
                                                     "horizontalalignment", "center", ...
                                                     "tag", "title_portz");
                                                     
main_GUI_handles.viewer_frame_pz_edit = uicontrol ("parent", main_GUI_fig, ...
                                                     "style", "edit", ...
                                                     "string", string(pz_edit_curZ), ...
                                                     "relief", "sunken", ...
                                                     "units", "pixels", ...    
                                                     "position", [128+gui_margin_x gui_margin_y+gui_frame_h-360 45 19], ... 
                                                     "BackgroundColor", [0.95 0.95 0.95],...
                                                     "fontunits", "points", ...
                                                     "fontsize", 12, ...
                                                     "horizontalalignment", "center", ...
                                                     "Max", 0, ...                                                     
                                                     "Min", 0, ...                                                                                                          
                                                     "tag", "title_portz",...
                                                     "Callback", "on_pzedit_click(main_GUI_handles)");                                                       
                                                     
main_GUI_handles.viewer_frame_pz_ohms = uicontrol ("parent", main_GUI_fig, ...
                                                     "style", "text", ...
                                                     "string", "Ohms", ...
                                                     "units", "pixels", ...    
                                                     "position", [175+gui_margin_x gui_margin_y+gui_frame_h-360 35 19], ... 
                                                     "fontunits", "points", ...
                                                     "fontsize", 12, ...
                                                     "horizontalalignment", "center", ...
                                                     "tag", "title_portz");                                                                                                          

///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
//
// Plot Button
//
//                                

main_GUI_handles.viewer_button_Plot = uicontrol (...
                                "parent", main_GUI_fig, ...
                                "style", "pushbutton", ...
                                "string", "Plot", ...
                                "units", "pixels", ...
                                "position", [80+gui_margin_x gui_margin_y+gui_padding_y gui_frame_w-160 40], ...
                                "fontname", gui_defaultfont, ...
                                "fontunits", "points", ...
                                "fontsize", 14, ...
                                "HorizontalAlignment","center", ...                                
                                "tag", "button_plot", ..
                                "Callback", "on_buttonPlot_click(main_GUI_handles)");          
                                
drawnow();      
                              
